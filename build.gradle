buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
		maven { url 'https://www.cursemaven.com' }
		maven { url "https://repo.spongepowered.org/repository/maven-public/" }
        jcenter()
        mavenCentral()
		gradlePluginPortal()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
		classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
		classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}
        
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

version = "1.12.2-1.7.3a"
group= "rafradek.TF2weapons"
archivesBaseName = "TF2-Stuff-Reloaded"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'
    
    accessTransformer = file('src/main/resources/META-INF/tf2_at.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'
        }

        server {

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'
        }
    }
}

mixin {
    add sourceSets.main, "mixins.tf2stuff-reloaded.refmap.json"
}

repositories {
    maven { url = 'https://repo.spongepowered.org/maven' }
    maven { url = 'https://www.cursemaven.com'}
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'
	implementation "org.spongepowered:mixin:0.8"
	implementation 'curse.maven:jei-238222:3043174'
	implementation 'curse.maven:dynamiclights-227874:2563244'
	implementation 'curse.maven:cofhcore-69162:2920433'
	implementation 'curse.maven:thermalfoundation-222880:2926428'
	implementation 'curse.maven:thermalexpansion-69163:2926431'
}

jar {
    manifest {
        attributes([
            "Specification-Title": "TF2weapons",
            "Specification-Vendor": "rafradek",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"rafradek",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
			"FMLAT": "tf2_at.cfg",
			"ForceLoadAsMod": "true",
			"FMLCorePluginContainsFMLMod": true,
			"TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
			"MixinConfigs": "mixins.tf2stuff-reloaded.json"
        ])
        
    }
}

shadowJar {
	classifier = ''
	exclude 'LICENSE.txt'
	dependencies {
		include(dependency("org.spongepowered:mixin:0.8"))
	}
}

build.dependsOn(shadowJar)

reobf {
    shadowJar {}
}

build.dependsOn(reobf)

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", "1.12.2"

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':"1.12.2"
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
